generic
    Max_N : Univ_Integer := 8;
package N_Queens is
    -- Place N queens on an NxN checkerboard so that none of them can
    -- "take" each other.

    type Chess_Unit is new Integer< -Max_N*2 .. Max_N*2 >;
        -- An integer big enough to represent values -Max_N*2 .. +Max_N*2

    subtype Row is Chess_Unit; -- A subrange in 1..Max_N
    subtype Column is Chess_Unit; -- A subrange
    type Solution is new Array<optional Column, Row>;  
      -- A "solution" is an array of Column's, indexed by "Row."
      -- It indicates in which Column a queen of the given Row is located
      -- An example solution would be:  2 8 6 1 3 5 7 4
      --   meaning that the queen in row 1 is at column 2,
      --   the queen in row 2 is at column 8, the queen in
      --   row 3 is at column 6, and so on.

    queued func Place_Queens(N : Row := Max_N) return Vector<Solution> 
      {for all Sol of Place_Queens => for all Col of Sol => Col not null};
        -- Produce a vector of solutions, with the requirement
        -- that for each solution, there are non-null column numbers
        -- specified for each row of the checkerboard.
end package N_Queens;

package body N_Queens is
    subtype Sum_Range is Chess_Unit; -- {Sum_Range in 2..2*Max_N};
        -- Sum_Range is used for diagonals where the row+column is the
        -- same throughout the diagonal.
    subtype Diff_Range is Chess_Unit; -- {Diff_Range in (1-Max_N) .. (Max_N-1)};
        -- Diff_Range is used for diagonals where row-column is the
        -- same throughout the diagonal.
    type Sum is new Countable_Set<Sum_Range>;
        -- This type of set keeps track of which Sum_Range diagonals
        -- have a queen on them already.
    type Diff is new Countable_Set<Diff_Range>;
        -- This type of set keeps track of which Diff_Range diagonals
        -- have a queen on them already.

    type Solution_State is record
        C : Column;    -- Current column
        Trial : Solution;  -- Trial solution, some col#s still null
        Diag_Sum : Sum;   -- Set of "sum" diagonals in use
        Diag_Diff : Diff; -- Set of "diff" diagnoals in use
    end record;

    -- We build up a solution state progressively as we move
    -- across the checkerboard, one column at a time.
    func Initial_State(N : Row) return Solution_State is
        return (C => 1, Trial => Create(1..N, null), 
          Diag_Sum => [], Diag_Diff => []);
    end func Initial_State;

    func Is_Acceptable(S : Solution_State; R : Row) return Boolean is
      -- Is_Acceptable returns True if the next queen could be
      -- place in row R.
        return S.Trial[R] is null and then
          (R+S.C) not in S.Diag_Sum and then 
          (R-S.C) not in S.Diag_Diff;
    end func Is_Acceptable;
    
    func Current_Column(S : Solution_State) return Column is
      -- Current_Column indicates which column we are working on.
        return S.C;
    end func Current_Column;

    func Next_State(S : Solution_State; R : Row) return Solution_State is
      -- Next_State returns a Solution_State produced by
      -- adding a queen at (Current_Column(S), R).
        return (C => S.C+1, 
          Trial     => S.Trial | [R => S.C],
          Diag_Sum  => S.Diag_Sum | (R+S.C),
          Diag_Diff => S.Diag_Diff | (R-S.C));
    end func Next_State;

    func Final_Result(S : Solution_State; R : Row) return Solution is
      -- Final_Result returns a result produced by adding a queen
      -- at (Columns.Last, R) to a solution with all other columns
      -- placed.
        return S.Trial | [R => S.C];
    end func Final_Result;

    func Partial_Solution(S : Solution_State) return Solution is
      -- Return partial solution thus far
        return S.Trial;
    end func Partial_Solution;

  exports

    queued func Place_Queens(N : Row := Max_N) return Vector<Solution> 
      {for all Sol of Place_Queens => for all Col of Sol => Col not null}
        -- Produce a vector of solutions, with the requirement
        -- that for each solution, there are non-null column numbers
        -- specified for each row of the checkerboard.
    is
      const Rows : Countable_Range<Chess_Unit> := 1..N;
      var Solutions : protected Vector<Solution> := [];
      
      Outer_Loop:
      for State : Solution_State := Initial_State(N) loop
          -- Iterate over the columns
        
          for R in Rows parallel loop
              -- Iterate over the rows
              if Is_Acceptable(State, R) then
                  -- Found a Row/Column combination that is not on any diagonal
                  -- already occupied.
                  if Current_Column(State) < N then
                      -- Keep going since haven't reached Nth column.
                      const Next := Next_State(State, R);
                      continue loop Outer_Loop with State => Next;
                  else
                      -- All done, remember trial result with last queen placed
                      const Final := Final_Result(State, R);
                      Solutions &= Final;
                  end if;
              end if;
          end loop;
      end loop Outer_Loop;
      return Solutions;
      
    end func Place_Queens;
end package N_Queens;

proc Test_N_Queens(N : Univ_Integer) is

    package Max_12_Queens is new N_Queens<Max_N => 12>;

    var Results := Max_12_Queens.Place_Queens(N);

    Put_Line("Number of results with " & N & " queens = " & |Results|);

    for each [I => Result] of Results forward loop

        Put("Result #" & I);

        for each Col of Result forward loop
            Put(" " & Col);
        end loop;
        Put('\n');
    end loop;

end proc Test_N_Queens;

